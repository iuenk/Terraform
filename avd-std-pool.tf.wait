# Create AVD Resource Group
resource "azurerm_resource_group" "avd_resource_group" {
  location = var.deploy_location
  name     = upper("${var.Customer}${var.avd_resource_group}")
}

# Create AVD workspace
resource "azurerm_virtual_desktop_workspace" "avd_std_workspace" {
  name                = "${var.Customer}${var.avd_std_workspace}"
  resource_group_name = azurerm_resource_group.avd_resource_group.name
  location            = azurerm_resource_group.avd_resource_group.location
  friendly_name       = "${var.Customer}${var.avd_std_workspace}"
  description         = "${var.Customer}${var.avd_std_workspace}"
}

resource "time_rotating" "avd_token" {
  rotation_days = 30
}

# Create AVD host pool
resource "azurerm_virtual_desktop_host_pool" "avd_std_hostpool" {
  resource_group_name      = azurerm_resource_group.avd_resource_group.name
  location                 = azurerm_resource_group.avd_resource_group.location
  name                     = "${var.Customer}${var.avd_std_hostpool}"
  friendly_name            = "${var.Customer}${var.avd_std_hostpool}"
  validate_environment     = true
  custom_rdp_properties    = "audiocapturemode:i:1;audiomode:i:0;autoreconnection enabled:i:1;networkautodetect:i:1"
  description              = "${var.Customer}${var.avd_std_hostpool}"
  type                     = var.avd_std_pool_type
  maximum_sessions_allowed = var.avd_std_max_user
  load_balancer_type       = var.avd_std_lbpool_type

  registration_info {
    expiration_date = time_rotating.avd_token.rotation_rfc3339
  }
}

# Create AVD DAG
resource "azurerm_virtual_desktop_application_group" "avd_std_dag" {
  resource_group_name = azurerm_resource_group.avd_resource_group.name
  host_pool_id        = azurerm_virtual_desktop_host_pool.avd_std_hostpool.id
  location            = azurerm_resource_group.avd_resource_group.location
  type                = "Desktop"
  name                = upper("${var.Customer}${var.avd_std_prefix}-dag")
  friendly_name       = "Desktop Application Group"
  description         = "AVD application group"
  depends_on          = [azurerm_virtual_desktop_host_pool.avd_std_hostpool, azurerm_virtual_desktop_workspace.avd_std_workspace]
}

#Associate Workspace and DAG
resource "azurerm_virtual_desktop_workspace_application_group_association" "ws-dag" {
  application_group_id = azurerm_virtual_desktop_application_group.avd_std_dag.id
  workspace_id         = azurerm_virtual_desktop_workspace.avd_std_workspace.id
}